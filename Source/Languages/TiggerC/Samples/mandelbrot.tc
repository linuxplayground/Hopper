//#define APPLE_I // rather than the default ACIA_6850 for E6502    
#define EXPERIMENTAL
#include "../system.tc"

func main() {
    
    writeString("\n Mandelbrot - ported from Gordon's TCBasic - Integers\n");
    writeString("    Ported to TiggerC.\n\n");
    writeChar(' ');
    
    byte[] start = millis();
    
    const char[] palette = ".,'~=+:;*%&$OXB#@ ";
    int a; int b; int c; int d;
    int q; int p; int t; int s; byte i;
    int y; int x;
    int f = 50;
    
    for (y = -12; y <= 12; y++) {
        for (x = -49; x <= 29; x++) {
            c = x * 229 / 100;
            d = y * 416 / 100;
            a = c; b = d; i = 0;
            while (true) {
                q = b / f; s = b - (q * f);
                t = ((a * a) - (b * b)) / f + c;
                b = 2 * ((a * q) + (a * s / f)) + d;
                a = t; p = a / f; q = b / f;
                if (((p * p) + (q * q)) >= 5) {
                    writeChar(palette[i]);
                    break;
                } else {
                    i++;
                    if (i < 16) {
                        continue;
                    }
                    writeChar(' ');
                    break;
                }
            }
        } // next x
        writeString("\n "); 
    } // next y
    
    word ms = elapsedMillis(start);
    word sec = elapsedSeconds(start);
    free(start);
    
    writeString("\n"); 
    writeWord(ms);writeString(" ms\n");        
    writeWord(sec);writeString(" s\n");        
    
    
}
