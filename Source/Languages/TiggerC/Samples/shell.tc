//#define APPLE_I
#define EXPERIMENTAL
#define ZEROPAGEGLOBALS
#include "../system"
#include "../file"

func ls(char[] path, bool recursive, byte indent) {
    byte[] dirHandle;
    byte[] entry;
    char[256] fullPath;
    char[256] childPath;
    bool isDir;
    byte i;

    // Get full path
    _getFullPath(fullPath, path);

    // Open directory
    dirHandle = opendir(fullPath);
    if (dirHandle == null) {
        puts(" ??");
        return;
    }

    // Read entries
    while (true) {
        entry = readdir(dirHandle);
        if (entry == null) { break;}
        
        isDir = ((entry[FileTypeOffset] & 0xF0) == FileTypeDirectory);

        putc('\n');
        i = indent;
        while (i != 0)
        {
            puts("    ");
            i--;
        }
        if (isDir)
        {
            putc('<');    
        }
        else
        {
            putc(' ');
        }
        puts((entry + FilenameOffset) as char[]);
        if (isDir)
        {
            putc('>');
        }

        // If entry is a directory and recursive flag is set
        if (recursive && isDir) {
            strcpy(childPath, fullPath);
            if (childPath[strlen(childPath) - 1] != '/') {
                strcat(childPath, "/");
            }
            strcat(childPath, (entry + FilenameOffset) as char[]);
            ls(childPath, true, indent+1); // Recursive call
        }
    }
    closedir(dirHandle);
}

func mkDir(char[] name) {
    if (0 != mkdir(name))
    {
        puts(" ??");
    }
}

func cd(char[] name) {
    if (0 != chdir(name))
    {
        puts(" ??");
    }
}

func cat(char[] name, bool writeMode) {
    byte[4096] buffer;
    word i;
    int  result;
    byte[] fh;
    char c;
    byte b;
    
    if (writeMode) {
        putc('\n');
        while (true) {
            c = getc();
            b = c as byte;
#ifdef APPLE_I
            b = b & 0x7F;
            c = b as char;
#endif            
            if (b == 0x1A) { break; } // <ctrl><Z>
            buffer[i++] = b;
            putc(c);
        }
        fh = fopen(name, "w");
        if (fh == null)
        {
            result = -1;
        }
        else
        {
            buffer[i-1] = 0;
            if (0 == fwrite(buffer, 1, i, fh))
            {
                result = -1;
            }
        }
    } else {
        fh = fopen(name, "r");
        if (fh == null)
        {
            result = -1;
        }
        else
        {
            i = fread(buffer, 1, 4095, fh);
            if (i != 0)
            {
                buffer[i] = 0;
                puts(buffer as char[]);
            }
        }
    }
    if (result != -1)
    {
        fclose(fh);    
    }
    else
    {
        puts(" ??");
    }
}

func main() {
    char[100] cmd;      // Buffer for command input
    char[256] path;
    word index;
    char c;
    byte b;
    putc('\n');
    
    chdir("/"); // initialize

    while (true) {
        index = 0;
        // newline
        getcwd(path, 256);
        putc('\n');
        puts(path);
        puts(" $ ");
        while (true) {
            c = getc();
            b = c as byte;
#ifdef APPLE_I
            b = b & 0x7F;
            c = b as char;
            c = tolower(c);
#endif
            if (b == 0x0A) { 
                cmd[index] = '\0';  // Null-terminate the command
                break; 
            } else if (b == 0x1A) {
                // <ctrl><Z>
                puts("\nExited\n");
                return;
            } else if (b == 0x08) {
                // backspace
                index--;
                putc(c);
                putc(' ');
                putc(c);
            } 
            else
            {
                cmd[index++] = c;
                putc(c);
            }
        }

        if (strncmp(cmd, "ls", 2) == 0) {
            strcpy(path, ".");
            bool recursive = false;
            
            // Parse arguments
            char[] args = cmd + 2;
            while (args[0] == ' ') { args++; }  // Skip spaces
            
            if (strncmp(args, "-R", 2) == 0) {
                recursive = true;
                args = args + 2;
                while (args[0] == ' ') { args++; } // Skip spaces
            }
        
            if (args[0] != (0 as char)) {
                strcpy(path, args);  // Copy path if provided
            }
            
            ls(path, recursive, 1);
            continue;
        } else if (strncmp(cmd, "mkdir ", 6) == 0) {
            strcpy(path, cmd + 6);
            mkDir(path);
            continue;
        } else if (strncmp(cmd, "cd ", 3) == 0) { // TODO "cd .." to root?
            strcpy(path, cmd + 3);
            cd(path);
            continue;
        } else if (strcmp(cmd, "mkfs") == 0) {
            format();
            chdir("/");
            continue;
        } else if (strncmp(cmd, "cat ", 4) == 0) {
            char[] space = strchr(cmd, ' ');
            if (space != null) {
                strcpy(path, space + 1);
                if (strchr(path, '>') != null) {
                    cat(path + 2, true);
                    continue;
                } else {
                    cat(path, false);
                    continue;
                }
            }
        } else if (strlen(cmd) == 0)
        {
            continue; // empty command
        }
        puts(" ??");
    }
}

