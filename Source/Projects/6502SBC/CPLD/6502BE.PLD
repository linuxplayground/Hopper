Name     6502BE ;
PartNo   00 ;
Date     6/07/2024 ;
Revision 01 ;
Designer Michael Cartwright ;
Company  None ;
Assembly None ;
Location  ;
Device   G22V10 ;


/* G20V8A  G22V10 */

/* Inputs:  All are signals from the 6502 */

Pin 1  =  CLK;
Pin 2  =  A15;
Pin 3  =  A14;
Pin 4  =  A13;
Pin 5  =  A12;
Pin 6  =  A11;
Pin 7  =  A10;
Pin 8  =  A9;
Pin 9  =  A8;
Pin 10 =  A7;
Pin 11 =  A6;
/* Pin 12 is GND */
Pin 13 =  A5;
Pin 14 =  A4;
/* WinCUPL doesn't seem to like 15 as an input */
Pin 16 =  A3;
Pin 17 =  A2;
Pin 23 =  A1;


/* Outputs:  define outputs - all are simple combinatorial */

Pin 22 = CSRAM;
Pin 21 = CSROM;
Pin 20 = CSVIA;
Pin 19 = CSACIA;
Pin 18 = OERAM;

IO      = A15 & A14 & !A13 & A12 & !A11 & !A10 & !A9 & !A8 & !A7 & !A6 & !A5; /* 0xD00x */

/* ACIA and VIA locations for Ben Eater 6502 board */

ACIA    = IO &  A4;                /* 0xD010 to 0xD013*/
VIA     = IO & !A4;                /* 0xD000 to 0xD00F */

CSACIA = !ACIA;
CSVIA  = !VIA;
CSRAM  = !(!A15 & CLK);
OERAM  = A15;
CSROM  = !(A15 & !IO);


