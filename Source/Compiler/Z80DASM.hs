program DASM
{
    #define JSON_EXPRESS // .code and .json are generated by us so assume .json files have no errors
    #define ASSEMBLER
    
    //#define EXPERIMENTAL
    
    uses "/Source/System/System"
    uses "/Source/System/Screen"
    
    uses "JSON/JSON"
    uses "JSON/Code"
    
    uses "CodeGen/Instructions"
    uses "CODEGEN/AsmZ80"
    
    uses "Tokens/Token"
    uses "Tokens/Scanner"
    uses "Tokens/SysCalls"
    uses "Tokens/LibCalls"
    uses "Tokens/Parser"
    
    bool extendedCodeSegment;
    
    uint codeSize = 0;
    uint instructionCount = 0;
    
    string pathLoaded;
    <string> sourceLines;
    string getSourceLine(string path, string lnum)
    {
        if (pathLoaded != path)
        {
            file sourceFile = File.Open(path);
            if (sourceFile.IsValid())
            {
                sourceLines.Clear();
                pathLoaded = path;
                loop
                {
                    string ln = sourceFile.ReadLine();
                    if (ln.Length == 0)
                    {
                        if (!sourceFile.IsValid())
                        {
                            break;
                        }
                    }
                    sourceLines.Append(ln);
                }
            }
        }
        string sourceLine;
        uint iline;
        if (UInt.TryParse(lnum, ref iline))
        {
            if (iline > 0)
            {
                iline--;
            }
            if (sourceLines.Count > iline)
            {
                sourceLine = sourceLines[iline];
            }
        }
        return sourceLine;
    }
    
    <byte> readIHex(file hexFile, ref uint org)
    {
        bool first = true;
        <byte> code;
        uint byteCount = 0;
        Parser.ProgressTick("i"); // readIHex
        loop
        {
            string ln = hexFile.ReadLine();
            if (!hexFile.IsValid()) { break; }
            string len = ln.Substring(1,2);
            uint length;
            _ = UInt.TryParse("0x" + len, ref length);
            if (length == 0) { continue; }
            if (first)
            {
                string orgString = ln.Substring(3,4);
                _ = UInt.TryParse("0x" + orgString, ref org);
            }
            ln = ln.Substring(9);
            while (length > 0)
            {
                string br = ln.Substring(0, 2);
                ln = ln.Substring(2);
                uint b;
                _ = UInt.TryParse("0x" + br, ref b);
                code.Append(byte(b));
                length--;
            }
            first = false;
            byteCount += 16;
            if (byteCount % 8192 == 0)
            {
                Parser.ProgressTick("i"); // readIHex
            }
        }
        return code;
    }
    
    BadArguments()
    {
        PrintLn("Invalid arguments for Z80DASM:");
        PrintLn("  Z80DASM <hexe file>");
        PrintLn("    -g <c> <r> : called from GUI, not console");
        PrintLn("    -b         : 'bare' - no hex, just assembly (for Chat GPT)");
    }
    
    {
        bool success = false;
        bool bare;
        loop
        {
            <string> rawArgs = System.Arguments;
            <string> args;
            for (uint iArg = 0; iArg < rawArgs.Count; iArg++)
            {
                string arg = rawArgs[iArg];
                if ((arg.Length == 2) && (arg[0] == '-'))
                {
                    arg = arg.ToLower();
                    switch (arg)
                    {
                        case "-g":
                        {
                            uint col;
                            uint row;
                            iArg++;
                            if (UInt.TryParse(rawArgs[iArg], ref col))
                            {
                            }
                            iArg++;
                            if (UInt.TryParse(rawArgs[iArg], ref row))
                            {
                            }
                            Parser.SetInteractive(byte(col), byte(row));
                        }
                        case "-b":
                        {
                            bare = true;
                        }
                        default:
                        {
                            args.Clear();
                            break;
                        }
                    }
                }
                else
                {
                    args.Append(arg);
                }
            }
          
            if (args.Count != 1)
            {
                BadArguments();
                break;
            }
            string ext = ".hex";
            string codePath = args[0];
            if (!File.Exists(ref codePath, ref ext, "/Bin/"))
            {
                BadArguments();
            }
            bool lastwasNOP = false;
            long startTime = Millis;
            loop
            {
                string extension = Path.GetExtension(codePath);
                string hasmPath  = codePath.Replace(extension, ".lst");
                hasmPath = Path.GetFileName(hasmPath);
                hasmPath = Path.Combine("/Debug/", hasmPath);
                File.Delete(hasmPath);

                file hasmFile = File.Create(hasmPath);
                if (!hasmFile.IsValid())
                {
                    PrintLn("Failed to create '" + hasmPath + "'");
                    break;
                }
                
                SysCalls.New();
                string symbolsPath  = codePath.Replace(extension, ".zcode");
                symbolsPath = Path.GetFileName(symbolsPath);
                symbolsPath = Path.Combine("/Debug/Obj", symbolsPath);
                
                if (File.Exists(symbolsPath))
                {
                    if (!ParseCode(symbolsPath, false, true))
                    {
                        break;
                    }
                }
                uint org;
                
                uint address = 0;
                file hexFile = File.Open(codePath);
                <byte> code = readIHex(hexFile, ref org);
                
                uint entryAddress = code[4] + (code[5] << 8);
                
                <uint, uint> methodSizes = Code.GetMethodSizes();
                uint indexMax = 0;
                foreach (var sz in methodSizes)
                {
                    if (sz.key > indexMax)
                    {
                        indexMax = sz.key;
                    }
                }
                <string,string> debugInfo;
                <uint,uint> methodFirstAddresses; // <address,index>
                <uint,uint> methodLastAddresses; // <address,index>
                
                uint prevIndex;
                for (uint index = 0; index <= indexMax; index++)
                {
                    <string,variant> methodSymbols = Code.GetMethodSymbols(index);
                    if (methodSymbols.Count != 0)
                    {
                        debugInfo = methodSymbols["debug"];
                        foreach (var kv in debugInfo)
                        {
                            uint codeAddress;
                            if (UInt.TryParse(kv.key, ref codeAddress))
                            {
                                if (index == 0)
                                {
                                    codeAddress = entryAddress;
                                }
                                else
                                {
                                    // -10 'ENTER'   preamble
                                    // -18 'ENTERB'  preamble (optimized only)
                                    // -14 'ENTERB' 1 ?
                                    // - 0'RETFAST'  preamble (optimized only)
                                    uint seek = codeAddress;
                                    loop
                                    {
                                        OpCode opCode2 = GetOpCode(code, seek-6);  
                                        OpCode opCode1 = GetOpCode(code, seek-4);  
                                        OpCode opCode0 = GetOpCode(code, seek);
                                            
                                        if ((opCode2 == OpCode.PUSH_IY)  && (opCode1 == OpCode.LD_inn_SP) && (opCode0 == OpCode.LD_IY_inn)) 
                                        {
                                            // "PUSH BP, BP = SP" stack frame setup
                                            //Print(AsmZ80.GetName(opCode2) + "   " + AsmZ80.GetName(opCode1) + "   " + AsmZ80.GetName(opCode0) + ",   ");
                                            codeAddress = seek - 6;
                                        }
                                        seek--;
                                        if (codeAddress - seek > 25) { break; }
                                    }
                                }
                                methodFirstAddresses[codeAddress] = index;
                                methodLastAddresses[prevIndex] = codeAddress-1;
                                
                                /*
                                if (index < 10)
                                {
                                    string name = methodSymbols["name"];
                                    PrintLn(index.ToHexString(4) + ": 0x" + codeAddress.ToHexString(4) + " " + name);
                                }
                                */
                                prevIndex = index;
                                break;
                            }
                        }
                    }
                }
                methodLastAddresses[prevIndex] = code.Count-1;
                
                /*
                foreach (var kv in methodFirstAddresses)
                {
                    uint index = kv.value;
                    uint startAddress = kv.key;
                    if (index < 10)
                    {
                        PrintLn(index.ToHexString(4) + ": 0x" + startAddress.ToHexString(4) + "-0x" + (methodLastAddresses[index]).ToHexString(4));
                    }
                }
                */
                
                uint doffset = 0;
                string src;
                string srcName;
                 
                uint index;
                uint currentMethodIndex = 0;
                uint instructionAddress;
                
                uint progressCount;
                loop
                {
                    if (index == code.Count) { break; }
                    instructionAddress = index;
                    
                    byte opCodeLength = GetOpCodeLength(code[index]);
                    
                    OpCode instruction;
                    OperandType operandType;
                    byte operandLength;
                    byte tableSize;
                    bool signed;
                    
                    //if (index + opCodeLength > code.Count) { hasmFile.Flush(); break; }
                    
                    if (opCodeLength == 1)
                    {
                         instruction = OpCode(code[index]);
                         index++;
                    }
                    else if (opCodeLength == 2)
                    {
                         instruction = OpCode((code[index] << 8) + code[index+1]);
                         index += 2;
                    }
                    string name = GetOpCodeInfo(instruction, ref operandType, ref operandLength, ref signed, false);
                    uint operand = 0;
                    if (name.Length == 0) { Print(" BadOp: 0x" + instructionAddress.ToHexString(4)); continue; }
                    
                    //if (index + operandLength > code.Count) { hasmFile.Flush(); break; }
                    
                    if (operandLength == 1)
                    {
                         operand = code[index];
                         index++;
                    }
                    else if (operandLength == 2)
                    {
                         operand = code[index] + (code[index+1] << 8);
                         index += 2;
                    }
                    codeSize += (opCodeLength + operandLength);
                    instructionCount++;
                    
                    if (instruction == OpCode.JP_HL)
                    {
                        tableSize = code[index];
                        index += 1;
                        codeSize += 1;
                    }
                    
                    // switch to next methods debugInfo:
                    uint switchAddress = 0;
                    if (entryAddress == instructionAddress)
                    {
                        switchAddress = entryAddress;
                    }
                    else if (instructionAddress-1 == methodLastAddresses[currentMethodIndex])
                    {
                        switchAddress = instructionAddress;
                    }
                    
                    if (methodFirstAddresses.Contains(switchAddress))
                    {
                        uint methodIndex = methodFirstAddresses[switchAddress];
                        <string,variant> methodSymbols = Code.GetMethodSymbols(methodIndex);
                        if (methodSymbols.Count != 0)
                        {
                            src = methodSymbols["source"];
                            srcName = Path.GetFileName(src);
                            string ln = methodSymbols["line"];
                            string nm = methodSymbols["name"];
                            debugInfo = methodSymbols["debug"];
                            doffset = instructionAddress;
                            hasmFile.Append("" + char(0x0A)); 
                            hasmFile.Append("// " + src + ":" + ln + char(0x0A));  
                            
                            string mname = "// ####  " + nm + "()  ####";
                            mname = mname.Pad(' ', 80);
                            mname = mname + "0x" + methodIndex.ToHexString(4) + char(0x0A);
                            hasmFile.Append(mname);  
                            hasmFile.Append("" + char(0x0A)); 
                            if (progressCount % 64 == 0)
                            {
                                Parser.ProgressTick("d");
                            }
                            progressCount++;
                        }
                        currentMethodIndex = methodIndex;
                    }
                    string comment;
                    string addressKey = "0x" + instructionAddress.ToHexString(4);
                    if (debugInfo.Contains(addressKey))
                    {
                        string debugLine = debugInfo[addressKey];
                        string sourceLine = getSourceLine(src, debugLine);
                        if (sourceLine.Length != 0)
                        {
                            comment = "// " + sourceLine.Trim();
                            if (comment.Length < 34)
                            {
                                comment = comment.Pad(' ', 34);
                                comment = comment + srcName + ":" + debugLine;
                            }
                        }
                        else
                        {
                            comment = "// " + src + ":" + debugLine;  
                        }
                    }
                    
                    string disassembly = AsmZ80.Disassemble(instructionAddress, instruction, operand, bare);
                    if (tableSize != 0)
                    {
                        disassembly = disassembly.Replace("JP HL", "JP HL              // " + tableSize.ToString() + " table entries follow:");
                    }
                    if (bare)
                    {
                        disassembly = disassembly.Substring(29);
                        if (disassembly == "NOP")
                        {
                            if (!lastwasNOP)
                            {
                                hasmFile.Append("" +char(0x0A));
                                lastwasNOP = true;
                            }
                        }
                        else
                        {
                            hasmFile.Append(disassembly + char(0x0A));
                            lastwasNOP = false;
                        }
                    }
                    else
                    {                  
                        hasmFile.Append(disassembly.Pad(' ', 48) + comment + char(0x0A));
                    }
                    if (tableSize > 0)
                    {
                        uint count = 0;
                        loop
                        {
                            if (tableSize == 0) { break; }
                            uint entry = code[index] + (code[index+1] << 8);
                            if (count % 8 == 0)
                            {
                                if (count != 0)
                                {
                                    hasmFile.Append("" + char(0x0A));
                                }
                                hasmFile.Append("                               ");
                            }
                            hasmFile.Append("0x" + entry.ToHexString(4) + " ");
                            index += 2;
                            codeSize += 2;
                            tableSize--;
                            count++;
                        }    
                        hasmFile.Append("" + char(0x0A));
                    }
                    //hasmFile.Flush(); // TODO REMOVE
                } // loop
                              
                Parser.ProgressTick("d");
                hasmFile.Flush();
                if (!Parser.IsInteractive())
                {
                    PrintLn();
                    Print("Success, " + codeSize.ToString() + " bytes of code, ", Colour.ProgressText, Colour.ProgressFace);
                    long elapsedTime = Millis - startTime;
                    float seconds = elapsedTime / 1000.0;
                    PrintLn("  " + seconds.ToString() +"s", Colour.ProgressHighlight, Colour.ProgressFace);
                    PrintLn("  " + instructionCount.ToString() + " instructions disassembled. ", Colour.ProgressText, Colour.ProgressFace);
                }
                else
                {
                    Parser.ProgressDone();
                }
                success = true;
                break;
            }
            break;
        }
        if (!success)
        {
            Diagnostics.SetError(0x0E);
        }
    }
}
